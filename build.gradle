import aQute.bnd.gradle.Bundle

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // See https://plugins.gradle.org/plugin/biz.aQute.bnd
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.2.0'
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
}

apply plugin: 'biz.aQute.bnd.builder'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group 'nl.intreq'
version '1.1.0'

ext {
    c4wTxaVersion = '1.1.0'
    groovyVersion = '3.0.11'
}

configurations {
    bundle {
        // Only put our own stuff in the bundle
        transitive = false
    }
}

repositories {
    mavenLocal()
    maven {
        name = 'gpr_c4w-txa-parser'
        url = "https://maven.pkg.github.com/swarringa/c4w-txa-parser/"
        content {
            includeModule "nl.intreq", "c4w-txa-parser"
        }
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    mavenCentral()
}

dependencies {
    implementation group: 'nl.intreq', name: 'c4w-txa-parser', version: c4wTxaVersion
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    implementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: groovyVersion
    implementation group: 'org.codehaus.groovy', name: 'groovy-test', version: groovyVersion
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    bundle "nl.intreq:c4w-txa-parser:$c4wTxaVersion"
    bundle "org.codehaus.groovy:groovy:$groovyVersion"
    bundle "org.codehaus.groovy:groovy-xml:$groovyVersion"
}

jar {
    def archiveVersion = project.version

    // This can be placed in a bnd.bnd file. But it is more convenient
    // to keep everything together here
    bundle {    
        bnd (
            'Bundle-Name'           : 'Intreq :: Clarion MultiDLL support',
            'Bundle-SymbolicName'   : 'nl.intreq.c4w.multidll',
            'Export-Package'        : "nl.intreq.c4w.multidll.*;ver=${archiveVersion}" ,
            '-exportcontents'       : 'nl.intreq.c4w.multidll.*'
        )
    }
}

// Creates OSGI manifest for the jar
task bundle(type: Bundle) {
    from sourceSets.main.output
}

publishing {
    publications {
        osgiBundle(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'GithubPackages'
            url = "https://maven.pkg.github.com/swarringa/c4w-multidll"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

test {
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
}