import aQute.bnd.gradle.Bundle

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // See https://github.com/bndtools/bnd/tree/master/biz.aQute.bnd.gradle
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.3.0'
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
}

apply plugin: 'biz.aQute.bnd.builder'

group 'nl.practicom'
version '0.1'

ext {
    groovyVersion = '2.4.17'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'nl.practicom:c4w-txa:0.1'
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-xml', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-test', version: groovyVersion
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    // This can be placed in a bnd.bnd file. But it is more convenient
    // to keep everything together here
    bnd (
            'Bundle-Name'           : 'Practicom :: Clarion MultiDLL support',
            'Bundle-SymbolicName'   : 'nl.practicom.c4w.multidll',
            'Export-Package'        : "nl.practicom.c4w.multidll.*;ver=${version}" ,
            '-exportcontents'       : 'nl.practicom.c4w.multidll.*'
    )
}

// Creates OSGI manifest for the jar
task bundle(type: Bundle) {
    from sourceSets.main.output
}

publishing {
    publications {
        osgiBundle(MavenPublication) {
            from components.java
        }
    }
}
